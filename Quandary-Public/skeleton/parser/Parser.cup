package parser;

import java.util.*;
import java.io.*;
import java_cup.runtime.Symbol;
import java_cup.runtime.ComplexSymbolFactory.ComplexSymbol;
import java_cup.runtime.ComplexSymbolFactory;

import ast.*;
import interpreter.Interpreter;

parser code {: 
  Lexer lexer;

  public Parser(Lexer lex, ComplexSymbolFactory sf) {
    super(lex, sf);
    lexer = lex;
  }

  public void report_fatal_error(String message, Object info) {
    Interpreter.fatalError(message + " (" + info + ")", Interpreter.EXIT_PARSING_ERROR);
  }

  ast.Location loc(int line, int column) {
    return new ast.Location(line, column);
  }

:};

terminal PLUS, MINUS, TIMES, COMMA, DOT;
terminal LPAREN, RPAREN, LBRACKET, RBRACKET, LCURLY, RCURLY;
terminal RETURN, PRINT, SEMI, MUT, NIL, CAST;
terminal EQUAL;
terminal IF, ELSE, WHILE;
terminal LESSEQUAL, LARGEEQUAL, NOTEQUAL, ISEQUAL, LESS, LARGER, AND, OR, NOT;
terminal INT, Q, REF;

terminal Long    INTCONST;
terminal String   IDENT;

non terminal Program         program;
non terminal Expr            expr;
non terminal BinaryExpr      binaryExpr;
non terminal Cond            cond;
non terminal Stmt            stmt;
non terminal List<Stmt>      stmtList;
non terminal FunctionDefinition     funcDef;
non terminal List<FunctionDefinition> funcDefList;
non terminal List<VarDecl>           formalDeclList;
non terminal List<VarDecl>           neFormalDeclList;
non terminal List<Expr>             exprList;
non terminal List<Expr>             neExprList;
non terminal VarDecl                  varDecl;
non terminal VarDecl.TYPE             type;
non terminal FunctionCallExpr         funcCallExpr;

precedence left PRINT;
precedence left IF;
precedence left ELSE;
precedence left AND, OR;
precedence left NOT;
precedence left LESSEQUAL, LARGEEQUAL, ISEQUAL, NOTEQUAL, LESS, LARGER;
precedence left DOT;
precedence left PLUS, MINUS;
precedence left TIMES;
precedence left CAST;

start with program;

program ::= funcDefList:fdl
            {: RESULT = new Program(fdl, loc(fdlleft, fdlright)); :}
         ;

funcDefList ::= funcDef:fd funcDefList:fdl 
               {: fdl.add(0, fd); RESULT = fdl; :}
            |  {: RESULT = new ArrayList<FunctionDefinition>(); :}    
            ;
            
funcDef ::= varDecl:v LPAREN formalDeclList:fdl RPAREN LCURLY stmtList:sl RCURLY
            {: RESULT = new FunctionDefinition(v, fdl, sl, loc(slleft, slright)); :}
         ;

varDecl ::= type:t IDENT:i
            {: RESULT = new VarDecl(false, t, i); :}
         | MUT type:t IDENT:i
            {: RESULT = new VarDecl(true, t, i); :}
         ;

type ::= INT
         {: RESULT = VarDecl.TYPE.INT; :}
      | Q
         {: RESULT = VarDecl.TYPE.Q; :}
      | REF
         {: RESULT = VarDecl.TYPE.REF; :}
      ;

formalDeclList ::= neFormalDeclList:nfdl
                     {: RESULT = nfdl; :}
                  |  {: RESULT = new ArrayList<VarDecl>(); :}
                  ;

neFormalDeclList ::= varDecl:v COMMA neFormalDeclList:nfdl
                     {: nfdl.add(0, v); RESULT = nfdl; :}
                  |  varDecl:v
                     {: List<VarDecl> nfd = new ArrayList<VarDecl>(); nfd.add(0, v); RESULT = nfd; :}
                  ;

stmtList ::= stmt:st stmtList:sl
            {: sl.add(0, st); RESULT = sl; :}
         |  {: RESULT = new ArrayList<Stmt>(); :}
         ;

stmt ::= varDecl:v EQUAL expr:e SEMI
         {: RESULT = new VarDeclStmt(v, e, loc(vleft, vright)); :}
      |  IDENT:i EQUAL expr:e SEMI
         {: RESULT = new DeclStmt(i, e, loc(ileft, iright)); :}
      |  IF cond:c stmt:s
         {: RESULT = new IfStmt(c, s, loc(cleft, cright)); :}
      |  IF cond:c stmt:s1 ELSE stmt:s2
         {: RESULT = new IfElseStmt(c, s1, s2, loc(cleft, cright)); :}
      |  WHILE LPAREN cond:c RPAREN stmt:s
         {: RESULT = new WhileStmt(c, s, loc(cleft, cright)); :}
      |  RETURN expr:e SEMI
         {: RESULT = new ReturnStmt(e, loc(eleft, eright)); :}
      |  PRINT expr:e SEMI
         {: RESULT = new PrintStmt(e, loc(eleft, eright)); :}
      |  LCURLY stmtList:sl RCURLY
         {: RESULT = new StmtList(sl, loc(slleft, slright)); :}
      |  funcCallExpr:fce SEMI
         {: RESULT = new FunctionCallStmt(fce, loc(fceleft, fceright)); :}
      ;

funcCallExpr ::= IDENT:i LPAREN exprList:el RPAREN
               {: RESULT = new FunctionCallExpr(i, el, loc(ileft, iright)); :}
            ;

exprList ::= neExprList:nel
                  {: RESULT = nel; :}
               |  {: RESULT = new ArrayList<Expr>(); :}
               ;

neExprList ::= expr:e COMMA neExprList:nel
                  {: nel.add(0, e); RESULT = nel; :}
               | expr:e
                  {: List<Expr> nel = new ArrayList<Expr>(); nel.add(0, e); RESULT = nel; :}
               ;

expr ::= NIL:n
         {: RESULT = new ConstExpr(loc(nleft, nright)); :}
      |  INTCONST:c
         {: RESULT = new ConstExpr(c, loc(cleft, cright)); :}
      |  IDENT:c
         {: RESULT = new IdentExpr(c, loc(cleft, cright)); :}
      |  LPAREN type:t RPAREN expr:e
         {: RESULT = new CastExpr(t, e, loc(tleft, tright)); :}
         %prec CAST
      |  funcCallExpr:fce
         {: RESULT = fce; :}
      |  binaryExpr:e
         {: RESULT = e; :}
      |  LBRACKET binaryExpr:e RBRACKET
         {: RESULT = new ConcurBinaryExpr(e, loc(eleft, eright)); :}
      |  LPAREN expr:e RPAREN
         {: RESULT = e; :}
      |  MINUS expr:e
         {: RESULT = new UnaryExpr(UnaryExpr.NEGATIVE, e, loc(eleft, eright)); :}
      ;

cond ::= expr:c1 LESSEQUAL expr:c2
         {: RESULT = new CompCond(c1, CompCond.LESSEQUAL, c2, loc(c1left, c1right)); :}
      |  expr:c1 LARGEEQUAL expr:c2
         {: RESULT = new CompCond(c1, CompCond.LARGEEQUAL, c2, loc(c1left, c1right)); :}
      |  expr:c1 ISEQUAL expr:c2
         {: RESULT = new CompCond(c1, CompCond.ISEQUAL, c2, loc(c1left, c1right)); :}
      |  expr:c1 NOTEQUAL expr:c2
         {: RESULT = new CompCond(c1, CompCond.NOTEQUAL, c2, loc(c1left, c1right)); :}
      |  expr:c1 LESS expr:c2
         {: RESULT = new CompCond(c1, CompCond.LESS, c2, loc(c1left, c1right)); :}
      |  expr:c1 LARGER expr:c2
         {: RESULT = new CompCond(c1, CompCond.LARGER, c2, loc(c1left, c1right)); :}
      |  cond:c1 AND cond:c2
         {: RESULT = new LogicalCond(c1, LogicalCond.AND, c2, loc(c1left, c1right)); :}
      |  cond:c1 OR cond:c2
         {: RESULT = new LogicalCond(c1, LogicalCond.OR, c2, loc(c1left, c1right)); :}
      |  NOT cond:c
         {: RESULT = new UnaryCond(UnaryCond.NOT, c, loc(cleft, cright)); :}
      |  LPAREN cond:c RPAREN
         {: RESULT = c; :}
      ;

binaryExpr ::= expr:e1 PLUS expr:e2
               {: RESULT = new BinaryExpr(e1, BinaryExpr.PLUS, e2, loc(e1left, e1right)); :}
            |  expr:e1 MINUS expr:e2
               {: RESULT = new BinaryExpr(e1, BinaryExpr.MINUS, e2, loc(e1left, e1right)); :}
            |  expr:e1 TIMES expr:e2
               {: RESULT = new BinaryExpr(e1, BinaryExpr.TIMES, e2, loc(e1left, e1right)); :}
            |  expr:e1 DOT expr:e2
               {: RESULT = new BinaryExpr(e1, BinaryExpr.DOT, e2, loc(e1left, e1right)); :}
            ;
